set shell := ["bash", "-uc"]
model_fname := shell("yq -0 .name model/*.linkml.yml | tr '-' '_'")
site_title := shell("yq -0 .title model/*.linkml.yml")

_default:
    @just --list --unsorted --justfile {{justfile()}}

# Build the project
[group("project")]
build: clean _post-process-linkml-schema generate-json-schema generate-documentation generate-example-data validate-example-data
    @echo "Building project…"
    @echo
    cp -r "artifacts/information_models" "artifacts/documentation/modules/schema/attachments/"
    cp -r "artifacts/schemas" "artifacts/documentation/modules/schema/attachments/"
    cp -r "artifacts/examples" "artifacts/documentation/modules/schema/attachments/"
    @echo "… OK."
    @echo
    @echo "All project artifacts have been generated and post-processed, and can found in: artifacts/"
    @echo

# Clean up the output directory
[group("project")]
clean:
    @echo "Cleaning up generated artifacts…"
    @echo
    @if [ -d "artifacts" ]; then \
        rm -rf "artifacts"; \
    fi
    mkdir -p "artifacts"
    @echo "… OK."
    @echo

# Post-process LinkML schema for preview or releasing
_post-process-linkml-schema:
    @echo "Copying source files to artifacts directory…"
    @echo
    mkdir -p "artifacts/information_models"
    cp "information_models/dp_meetdata.schema.linkml.yml" "artifacts/information_models/"
    @echo
    @echo "Setting version in LinkML schema…"
    @echo
    sed -i '/^version: .*$/d' "artifacts/information_models/dp_meetdata.schema.linkml.yml"
    @if [ -z ${VERSION:-} ]; then \
        sed -i "/^name: .*$/a version: {{shell(ref_name)}}" "artifacts/information_models/dp_meetdata.schema.linkml.yml"; \
    else \
        sed -i "/^name: .*$/a version: ${VERSION}" "artifacts/information_models/dp_meetdata.schema.linkml.yml"; \
    fi
    @echo "… OK."
    @echo

# Edit the schema
[group("schema")]
edit-schema:
    @${VISUAL:-${EDITOR:-nano}} model/{{name}}.linkml.yml

# Show definition of the class identified by the provided CURIE
[group("schema")]
get-definition curie:
    yq '.classes.* | select(.class_uri == "{{curie}}")' information_models/dp_meetdata.schema.linkml.yml

# List all classes in the schema
[group("schema")]
list-classes:
    yq '.classes.* | key' model/{{name}}.linkml.yml

# Finish draft
[group("version-control")]
finish-draft:
    @echo "Finishing draft…"
    @echo
    @echo "Marking draft pull request as ready for review…"
    @echo
    gh pr ready
    @echo "… OK."
    @echo

# Release new major version
[group("version-control")]
release-major-version: #generate-documentation validate-example-data
    #!/bin/env bash
    git fetch && git fetch --tags

    latest_major=$(git tag -l | grep -P '^\d+\.\d+$' | cut -d . -f 1 | sort -n | tail -1)
    if [ -z "$latest_major" ]; then
        git tag 1.0;
    else
        git tag $(echo $latest_major + 1 | bc).0;
    fi

    git push --tags

    echo "… OK."
    echo

# Release new minor version
[group("version-control")]
release-minor-version:
    #!/bin/env bash
    git fetch && git fetch --tags

    latest_version=$(git tag -l | grep -P '^\d+\.\d+$' | sort -t . -k 1,1n -k 2,2n | tail -1)
    if [ -z "$latest_version" ]; then
        git tag 1.0;
    else
        latest_major=$(echo $latest_version | cut -d . -f 1);
        latest_minor=$(echo $latest_version | cut -d . -f 2);
        new_minor=$(echo $latest_minor + 1 | bc);
        git tag $latest_major.$new_minor;
    fi

    git push --tags

    echo "… OK."
    echo

# Increment major version
[group("project")]
increment-major-version:
    #!/bin/env bash
    git fetch && git fetch --tags

    latest_major=$(git tag -l | grep -P '^\d+\.\d+$' | cut -d . -f 1 | sort -n | tail -1)
    if [ -z "$latest_major" ]; then
        new_major=1.0;
    else
        new_major=$(echo $latest_major + 1 | bc).0;
    fi

    echo "New major version is: $new_major"
    echo
    echo "Writing new major version to LinkML schema…"
    yq -i '.version = env(version)' $OUTDIR/antora.yml

    echo "OK."
    echo


# Generate documentation
[group("generators")]
generate-documentation: _post-process-linkml-schema
    @echo "Generating documentation…"
    @echo
    cp -r "documentation" "artifacts"
    mkdir -p "artifacts/documentation/modules/schema"
    poetry run python -m linkml_asciidoc_generator.main \
        "artifacts/information_models/dp_meetdata.schema.linkml.yml" \
        "artifacts/documentation/modules/schema"
    echo "- modules/schema/nav.adoc" >> artifacts/documentation/antora.yml
    @echo "… OK."
    @echo
    @echo -e "Generated documentation files at: artifacts/documentation"
    @echo

# Generate example data
[group("generators")]
generate-example-data: _post-process-linkml-schema
    @echo "Generating JSON example data…"
    @echo
    mkdir -p "artifacts/examples"
    for example_file in examples/*.yml; do \
        [ -f "$example_file" ] || continue; \
        poetry run gen-linkml-profile  \
            convert \
            "$example_file" \
            --out "artifacts/${example_file%.*}.json"; \
    done
    @echo "… OK."
    @echo
    @echo -e "Generated example JSON data at: artifacts/examples"
    @echo

# Generate JSON Schema
[group("generators")]
generate-json-schema: _post-process-linkml-schema
    @echo "Generating JSON Schema…"
    @echo
    mkdir -p "artifacts/schemas/json_schema"
    poetry run gen-json-schema \
        --not-closed \
        "artifacts/information_models/dp_meetdata.schema.linkml.yml" \
        > "artifacts/schemas/json_schema/dp_meetdata.json_schema.json"
    @echo "… OK."
    @echo
    @echo "Generated JSON Schema at: artifacts/schemas/json_schema/dp_meetdata.json_schema.json"
    @echo

# Validate example data
[group("validate")]
validate-example-data: generate-json-schema generate-example-data
    @echo "Validating example data against JSON schema…"
    @echo
    for example_file in artifacts/examples/*.json; do \
        [ -f "$example_file" ] || continue; \
        poetry run check-jsonschema --schemafile "artifacts/schemas/json_schema/dp_meetdata.json_schema.json" $example_file; \
    done
    @echo "… OK."
    @echo

# Serve the generated website
[group("project")]
serve:
    http-serve output/html
